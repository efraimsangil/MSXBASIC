10 DEFINT A-Z:KEY OFF:COLOR 15,1,15:SCREEN 1,1,0:WIDTH 32
15 FOR I=0 TO 15:VPOKE 14336+I,VPEEK(8+I):NEXT I
20 ON SPRITE GOSUB 60:GOSUB 80
25 HX=120:HY=88:EX=8:EY=8
30 TIME=0:VPOKE 6912,HY-1:VPOKE 6913,HX
35 VPOKE 6916,EY-1:VPOKE 6917,EX:J=STICK(0)
40 HY=HY+2*(J=1ANDHY>8)-2*(J=5ANDHY<168)
45 HX=HX+2*(J=7ANDHX>8)-2*(J=3ANDHX<232)
50 EX=EX-(EX<HX)+(EX>HX):EY=EY-(EY<HY)+(EY>HY)
55 IF TIME<3 THEN 55 ELSE 30
60 GOSUB 80:LOCATE 0,1
65 FOR I=0 TO 21:TIME=0:LOCATE 9:PRINT"*** GOTCHA ***";CHR$(7):COLOR 1+14*(I MOD 2)
70 IF TIME<3 THEN 70 ELSE NEXT I:COLOR 15
75 IF NOT STRIG(0) THEN 75 ELSE CLS:RETURN 20
80 SPRITE OFF:PUT SPRITE 1,(0,-17),15,0:PUT SPRITE 0,(32,-17),15,1:SPRITE ON:RETURN
