10 ' *** GRADES CHART PROGRAM ***
20 '
30 DIM A(3,4)
40 CLS
50 '
60 '*** ENTER GRADES ***
70 FOR I=0 TO 2
80 FOR J=0 TO 2
90 ON I+1 GOTO 100,120,140
100 LOCATE 0,J:PRINT "Reading, term ";J+1;
110 INPUT A(0,J):GOTO 160
120 LOCATE 0,J+3:PRINT "Writing, term ";J+1;
130 INPUT A(1,J):GOTO 160
140 LOCATE 0,J+6:PRINT "Math, term ";J+1;
150 INPUT A(2,J)
160 NEXT J
170 NEXT I
180 '
190 ' **CALCULATE TOTALS AND AVERAGES**
200 FOR J=0 TO 2
210 T=0
220 FOR I=0 TO 2
230 T=T+A(I,J)
240 NEXT I
250 A(3,J)=T
260 NEXT J
270 FOR I=0 TO 3
280 T=0
290 FOR J=0 TO 2
300 T=T+A(I,J)
310 NEXT J
320 A(I,3)=T
330 A(I,4)=INT(A(I,3)/3)
340 NEXT I
350 '
360 ' *** MAKE CHART ***
370 CLS
380 LOCATE 5,0
390 PRINT "READ WRITE MATCH  TOTAL"
400 FOR S=1 TO 3
410 LOCATE 2,S+1:PRINT S
420 NEXT S
430 LOCATE 1,5:PRINT "TTL"
440 LOCATE 1,6:PRINT "AVR"
450 FOR I=0 TO 3
460 FOR J=0 TO 4
470 LOCATE I*6+4,J+2
480 PRINT A(I,J)
490 NEXT J
500 NEXT I

