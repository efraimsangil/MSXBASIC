05 SCREEN 0 : WIDTH 40
10 DIM MAPA$(8,7):DIM PILHA(100,2):TP=0
20 FOR Y=1 TO 7:FOR X=1 TO 7
30 READ A : MAPA$(X,Y)=CHR$(A)
40 NEXT X,Y
50 GOSUB 500
55 PRINT:PRINT"Tecle algo ...":A$=INPUT$(1)
60 ' Estabelece ponto de partida, a cor e nova_cor
70 TP=1 : PILHA(TP,1)=4 : PILHA(TP,2)=4 ' Push X,Y
80 CO$="1":NC$="4"
90 ' Loop de controle
100 GOSUB 200
110 IF TP<>0 THEN 100
120 GOSUB 500
130 END
200 ' Funcao fill
210 X=PILHA(TP,1):Y=PILHA(TP,2):TP=TP-1 ' Pop
220 IF X<1 OR X>7 OR Y<1 OR Y>7 THEN RETURN
230 IF MAPA$(X,Y) <> CO$ THEN RETURN
240 MAPA$(X,Y)=NC$
250 TP=TP+1 : PILHA(TP,1)=X+1 : PILHA(TP,2)=Y    ' Push X+1,Y
260 TP=TP+1 : PILHA(TP,1)=X-1 : PILHA(TP,2)=Y    ' Push X-1,Y
270 TP=TP+1 : PILHA(TP,1)=X   : PILHA(TP,2)=Y+1  ' Push X,Y+1
280 TP=TP+1 : PILHA(TP,1)=X   : PILHA(TP,2)=Y-1  ' Push X,Y-1
290 RETURN
500 ' Imprime mapa
510 FOR Y=1 TO 7:FOR X=1 TO 7
520 LOCATE X,Y:PRINT MAPA$(X,Y);
530 NEXT X,Y
540 RETURN
600 ' Dados mapa
610 DATA 32,32,49,49,49,32,32
620 DATA 32,49,49,49,49,49,32
630 DATA 49,49,49,49,49,49,49
640 DATA 49,49,49,49,49,49,49
650 DATA 49,49,49,49,49,49,49
660 DATA 32,49,49,49,49,49,32
670 DATA 32,32,49,49,49,32,32
