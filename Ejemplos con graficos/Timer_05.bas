10 SET BEEP 1,4:COLOR=(6,2,0,0)
20 BLOAD"XBASIC.BIN",R:_TURBO ON
30 DEFINTA-Z:DIM T(4):COLOR 15,0,0:FOR I=0 TO 255:VPOKE&H800+I,0:NEXT I
40 IF PEEK(&HD003)=13THEN T(1)=PEEK(&HD000):T(2)=PEEK(&HD001):T(3)=PEEK(&HD002)
50 Y=1:MX=23:CLS:PRINT"Setup timer":VPOKE&H80B,224:VDP(13)=&HF4:VDP(14)=&HF0
60 PRINT"Hours  :";T(1):PRINT"Minutes:";T(2):PRINT"Seconds:";T(3)
70 'A=STICK(0):T(Y)=T(Y)-((A=3)AND(T(Y)0))
75 A=STICK(0):T(Y)=T(Y)-((A=3)AND(T(Y)<MX))+((A=7)AND(T(Y)>0))
80 IF INKEY$=CHR$(13) OR A=5 THEN VPOKE&H800+Y*10+1,0:Y=Y+1:IF Y<4 THEN MX=59:VPOKE&H800+Y*10+1,224 ELSE 110
90 TIME=0
100 IF TIME<4 GOTO 100 ELSE LOCATE 8,Y:PRINTT(Y):GOTO 70
110 CLS:POKE&HD003,13:POKE&HD000,T(1):POKE&HD001,T(2):POKE&HD002,T(3)
120 H$=STR$(T(1)):M$=STR$(T(2)):S$=STR$(T(3))
130 IF LEN(H$)<3 THEN H$="0"+RIGHT$(H$,1) ELSE H$=RIGHT$(H$,2)
140 IF LEN(M$)<3 THEN M$="0"+RIGHT$(M$,1) ELSE M$=RIGHT$(M$,2)
150 IF LEN(S$)<3 THEN S$="0"+RIGHT$(S$,1) ELSE S$=RIGHT$(S$,2)
160 IF T(1)=0 AND T(2)=0 AND T(3)<10 THEN BEEP:COLOR 15,6,6
170 I=160:CF=CF XOR 156:VPOKE I+25,CF:VPOKEI+54,CF:I=I+240:VPOKE I+25,CF:VPOKEI+54,CF
180 O$=H$+M$+S$:FORY=0TO7:X1=0:Y1=Y*80:FOR X=1TO6:CH=ASC(MID$(O$,X,1))
190 MS=128:CD=PEEK(&H1BBF+CH*8+Y):X1=X1+12:IF (X=3) OR (X=5) THEN X1=X1+5
200 FORI=0TO5:IFCDANDMSTHEN C=CH ELSE C=0
210 VPOKEY1+X1+I*2-12,C:VPOKEY1+X1+I*2-11,C:MS=MS/2:NEXT I:IF STRIG(0) GOTO 260 ELSE NEXT X,Y
220 TM=TIME/60:IF TM=OT GOTO 220 ELSE OT=TM
230 T(3)=T(3)-1:IF T(3)>=0 GOTO 120 ELSE T(3)=59
240 T(2)=T(2)-1:IF T(2)>=0 GOTO 120 ELSE T(2)=59
250 T(1)=T(1)-1:IF T(1)>=0 GOTO 120
260 FOR I=0 TO 10:BEEP:NEXT I:CLS:COLOR 15,0,0
270 _TURBO OFF
