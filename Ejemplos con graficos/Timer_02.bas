10 FG=15:BG=1
20 LINE INPUT"Please enter timer time in seconds:";T$:T=VAL(T$)
30 ON INTERVAL=60-10*(PEEK(&H2B)\128) GOSUB 100:ON STOP GOSUB 70: STOP ON
40 COLOR FG,BG,BG: SCREEN 3: OPEN"GRP:" AS 1: PRESET(4,56):PRINT#1,"  :  :  "
50 INTERVAL ON
60 IF T>0 THEN 60
70 INTERVAL OFF:SCREEN0:COLOR15,4,4
80 IF T=0 THEN PRINT"Time's up!":BEEP:BEEP:BEEP
90 END
100 H=T\3600:M=(T\60)MOD60:S=TMOD60
110 H$=STR$(H+100):H1$=MID$(H$,3,1):H2$=MID$(H$,4,1)
120 M$=STR$(M+100):M1$=MID$(M$,3,1):M2$=MID$(M$,4,1)
130 S$=STR$(S+100):S1$=MID$(S$,3,1):S2$=MID$(S$,4,1)
140 IF H1$<>H3$ THEN COLORBG:PRESET(4  ,56):PRINT#1,H3$:COLORFG:PRESET(4  ,56):PRINT#1,H1$:H3$=H1$
150 IF H2$<>H4$ THEN COLORBG:PRESET(36 ,56):PRINT#1,H4$:COLORFG:PRESET(36 ,56):PRINT#1,H2$:H4$=H2$
160 IF M1$<>M3$ THEN COLORBG:PRESET(100,56):PRINT#1,M3$:COLORFG:PRESET(100,56):PRINT#1,M1$:M3$=M1$
170 IF M2$<>M4$ THEN COLORBG:PRESET(132,56):PRINT#1,M4$:COLORFG:PRESET(132,56):PRINT#1,M2$:M4$=M2$
180 IF S1$<>S3$ THEN COLORBG:PRESET(196,56):PRINT#1,S3$:COLORFG:PRESET(196,56):PRINT#1,S1$:S3$=S1$
190 IF S2$<>S4$ THEN COLORBG:PRESET(228,56):PRINT#1,S4$:COLORFG:PRESET(228,56):PRINT#1,S2$:S4$=S2$
200 T=T-1
210 RETURN
