10 VDP(14)=0:COLOR 11,0,0:SCREEN ,,0,,,3:DEFINT A-Z:KEYOFF
20 POKE&HFBB1,1
30 PRINT"Matrix Font Loading ...":BLOAD"XBASIC.BIN",R:BLOAD"font.FNT",S
40 _TURBO ON
50 DEFINT A-Z:DIM X(31),F(31),M(31),R(31),G(8):S=15:I=RND(-TIME)
55 ' Jump to skip Interrupt routine
60 GOTO 190
65 ' Interrupt hook jumper
70 GOTO 80
80 '		(Interrupt routine)
90 '
100 IF NI=0 GOTO 120 ELSE NI=(NI+1) AND 15
110 RETURN
120 ' 		(Interrupt handler)
130 '
135 ' Set font bank
140 NI=1:VDP(4)=3+((VDP(-2)AND2)>0):NI=0
145 ' Interrupt Return (reti)
150 RETURN
160 '
170 '			( Install Interrupt Vector )
180 ' 
190 '	DI		Load address of Basic string 70 to VH
200 '  #I &hF3,&h21,@70,&h22,VH
205 ' Write VH to Hook
210 FOR I=0 TO 3:POKE &HFD9A+I,PEEK(VH+I):NEXT I
220 '   EI
230 '#I &hFB
240 '
250 '			(User Program Start Point)
260 '
270 CLS:COLOR 2,0,0:KEY OFF:COLOR=(2,1,4,3):COLOR=(12,5,7,6):COLOR=(0,0,1,1)
280 'CLS:COLOR 2,0,0:KEY OFF:COLOR=(2,2,3,5):COLOR=(12,4,5,7):COLOR=(0,0,0,1)
290 VDP(13)=&HC0:VDP(14)=&HF0
300 FOR I=0 TO 5:R(I)=32:F(I)=RND(1)*80:X(I)=F(I):NEXT I
310 'FOR I=0 TO 240:VPOKE(&H800+I),0:NEXT I:I=0
320 M=1:FOR I=0 TO 7:G(I)=M:M=M*2:NEXT I:I=0
330 M(I)=G(7-(F(I) AND 7))
340 AD=&H800+F(I)/8
350 IF R(I)<>32 THEN AD=AD+10:VPOKE(I),32:GOTO 410
360 VPOKE AD,VPEEK(AD) OR M(I)
370 C=((C+TIME) AND 255):IF (C=32) OR (C>253) GOTO 370
380 VPOKE(F(I)),C:IF TIME AND 3 GOTO 480
390 D=D+33:IF D>1920 THEN D=D-1920
400 K=VPEEK(D):IF (K<>31)AND (K<>32) AND (K<253) THEN VPOKE (D),K+1
410 VPOKE AD,VPEEK(AD) XOR M(I)
420 F(I)=F(I)+80:IF F(I)<1920 GOTO 470
425 'Set new start point
430 J=0:T=TIME AND 127:IF T>79 THEN T=79-(127-T)
440 IF (T=X(J)) AND (I<>J) THEN 430
450 J=J+1:IF J<=5 GOTO 440
460 X(I)=T:F(I)=T:R(I)=VPEEK(F(I))
470 IF STRIG(0) GOTO 490
480 I=(I+1) AND 5:GOTO 330
490 '
500 '
510 '
520 '			Exit Function
530 CLS:COLOR 11,0,0
540 '		Remove Interrupt hook (Set RET code)
550 POKE &HFD9A,&HC9
560 '		Restore font data from EEPROM
570 FOR I=0 TO &H800:VPOKE(&H1000+I),PEEK(&H1BBF+I):NEXT I:VDP(4)=2
571 FOR I=0 TO 240:VPOKE(&H800+I),0:NEXT I:I=0
580 _TURBO OFF
585 ' Clear keyboard buffer
590 POKE &HF3FA,PEEK(&HF3F8)
600 POKE &HF3FB,PEEK(&HF3F9):POKE &HFBB1,0:END

